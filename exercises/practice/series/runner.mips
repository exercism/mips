#
# Test slices with some examples
#
# a0 - series string, for callee
# a1 - slice length, for callee
# a2 - pointer to output string, for callee
# s0 - num of tests left to run
# s1 - address of series string
# s2 - address of slice length
# s3 - address of expected output string
# s4 - copy of output location
# s5 - copy of address of expected output

.eqv BUFFER_SIZE 56

.data

# number of test cases
n:  .word 11
series:  .asciiz
        "1",
        "12",
        "35",
        "9142",
        "777777",
        "918493904243",
        "12345",
        "12345",
        "12345",
        "123",
        ""
slice_length:  .word
        1,
        1,
        2,
        2,
        3,
        5,
        6,
        42,
        0,
        -1,
        1
# expected output values (null terminated)
outs:  .asciiz
        "1",
        "1, 2",
        "35",
        "91, 14, 42",
        "777, 777, 777, 777",
        "91849, 18493, 84939, 49390, 93904, 39042, 90424, 04243",
        "",
        "",
        "",
        "",
        ""

failmsg: .asciiz "failed for test input: ("
commamsg: .asciiz ", "
expectedmsg: .asciiz "). expected "
tobemsg: .asciiz " to be "
okmsg: .asciiz "all tests passed"


.text

runner:
        lw      $s0, n
        la      $s1, series
        la      $s2, slice_length
        la      $s3, outs

        li      $v0, 9                  # code for allocating heap memory
        li      $a0, BUFFER_SIZE        # specify length of longest expected output
        syscall
        move    $s4, $v0                # location of allocated memory

run_test:
        jal     clear_output            # zero out output location

        move    $a0, $s1                # load address of series string
        lw      $a1, 0($s2)             # load slice length
        move    $a2, $s4                # load output address
        jal     slices                  # call subroutine under test

        move    $t0, $s4                # set a temporary pointer to allocated memory
        move    $s5, $s3                # copy pointer to current expected output string
scan:
        lb      $t3, 0($s3)             # load one byte of the expectation
        lb      $t4, 0($t0)             # load one byte of the actual
        bne     $t3, $t4, exit_fail     # if the two differ, the test has failed
        addi    $s3, $s3, 1             # point to next expectation byte
        addi    $t0, $t0, 1             # point to next actual byte
        bne     $t3, $zero, scan        # if one char (and therefore the other) was not null, loop

input_scan:
        lb      $t1, 0($s1)
        addi    $s1, $s1, 1
        bne     $t1, $zero, input_scan

done_scan:
        addi    $s2, $s2, 4             # point to next slice length word
        sub     $s0, $s0, 1             # decrement num of tests left to run
        bgt     $s0, $zero, run_test    # if more than zero tests to run, jump to run_test

exit_ok:
        la      $a0, okmsg              # put address of okmsg into a0
        li      $v0, 4                  # 4 is print string
        syscall

        li      $v0, 10                 # 10 is exit with zero status (clean exit)
        syscall

exit_fail:
        la      $a0, failmsg            # put address of failmsg into a0
        li      $v0, 4                  # 4 is print string
        syscall

        move    $a0, $s1                # load address of series string
        li      $v0, 4
        syscall

        la      $a0, commamsg
        li      $v0, 4
        syscall

        lw      $a0, 0($s2)             # load slice length
        li      $v0, 1
        syscall

        la      $a0, expectedmsg
        li      $v0, 4
        syscall

        move    $a0, $s4                # print actual that failed on
        li      $v0, 4
        syscall

        la      $a0, tobemsg
        li      $v0, 4
        syscall

        move    $a0, $s5                # print expected value that failed on
        li      $v0, 4
        syscall

        li      $a0, 1                  # set error code to 1
        li      $v0, 17                 # 17 is exit with error
        syscall

clear_output:
                                        # zero out output by storing zeros
        addi    $t0, $s4, BUFFER_SIZE   # pointer to end of output buffer

clear:
        subi    $t0, $t0, 4             # decrement pointer
        sw      $zero, 0($t0)           # store a 0 word
        bne     $t0, $s4, clear         # repeat util we have reached the start of the buffer
        jr      $ra

# # Include your implementation here if you wish to run this from the MARS GUI.
# .include "impl.mips"
