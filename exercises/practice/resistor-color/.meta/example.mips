# Convert ascii string of color code to an integer
#
# Strategy: Identify the only possible color using the first letter, then another letter if necessary.
#
# $a0 - input, pointer to null-terminated color code string
# $v0 - output, numeric value
# $t0 - character from input string
# $t1 - known color code character for comparison

.globl color_code

.text

color_code:
        lb      $t0, 0($a0)             # load first byte
        li      $t1, 'b'
        beq     $t0, $t1, black_brown_blue
        li      $t1, 'g'
        beq     $t0, $t1, green_grey
        li      $t1, 'r'
        ble     $t0, $t1, orange_red
        li      $t1, 'v'
        beq     $t0, $t1, violet
        li      $t1, 'w'
        beq     $t0, $t1, white

yellow:
        li      $v0, 4
        jr      $ra

black_brown_blue:
        lb      $t0, 2($a0)             # load third byte
        li      $t1, 'a'
        beq     $t0, $t1, black
        li      $t1, 'o'
        beq     $t0, $t1, brown

blue:
        li      $v0, 6
        jr      $ra

green_grey:
        lb      $t0, 3($a0)             # load fourth byte
        li      $t1, 'e'
        beq     $t0, $t1, green

grey:
        li      $v0, 8
        jr      $ra

orange_red:
        li      $t1, 'o'
        beq     $t0, $t1, orange

red:
        li      $v0, 2
        jr      $ra

violet:
        li      $v0, 7
        jr      $ra

white:
        li      $v0, 9
        jr      $ra

black:
        li      $v0, 0
        jr      $ra

brown:
        li      $v0, 1
        jr      $ra

green:
        li      $v0, 5
        jr      $ra

orange:
        li      $v0, 3
        jr      $ra
